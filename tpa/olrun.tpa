DEFINE_ACTION_FUNCTION ~olrun~ BEGIN

// When launching like this vs top level tp2, it seems to want
// the exact output name for the COPY command.
COPY ~%MOD_FOLDER%/portraits/olrun.pngl.bmp~ ~override/uxolrl.bmp~
COPY ~%MOD_FOLDER%/portraits/olrun.pngm.bmp~ ~override/uxolrm.bmp~
COPY ~%MOD_FOLDER%/portraits/olrun.pngs.bmp~ ~override/uxolrs.bmp~

// Add the items
COPY ~%MOD_FOLDER%/items/uxham01.itm~ ~override~
SAY NAME1 ~Olrun's Hammer of Smithing~
SAY NAME2 ~Olrun's Hammer of Smithing~
SAY DESC ~This is Olrun's "Hammer of Smithing".
The 'h' is silent.

STATISTICS:

Damage: 1d4+3 (crushing)
Speed Factor: 2
Proficiency Type: Hammers
Type: One-handed
Requires:
 Olrun

Weight: 3
~
SAY UNIDENTIFIED_DESC ~The Hammer of Smithing~
// WRITE_ASCII 0x0022 ~S3~ #2
LPF DELETE_ITEM_EQEFFECT INT_VAR silent=1 opcode_to_delete=0x7 END
LPF DELETE_ITEM_EQEFFECT INT_VAR silent=1 opcode_to_delete=0x8 END
// Color
LPF ADD_ITEM_EQEFFECT INT_VAR silent=1 opcode=0x7 parameter1=0x60 parameter2=16 target=1 timing=2 END
LPF ADD_ITEM_EQEFFECT INT_VAR silent=1 opcode=0x7 parameter1=0x3a parameter2=21 target=1 timing=2 END

COPY ~%MOD_FOLDER%/items/uxlea02.itm~ ~override~
SAY NAME1 ~Olrun's Armor~
SAY NAME2 ~Olrun's Armor~
SAY DESC ~This is Olrun's personal armor.
When he's wearing this, he is ready to rage.

STATISTICS:

Armor Class: 3 (1 vs piercing, 2 vs missile)

Requires:
 Olrun

Weight: 3
~
SAY UNIDENTIFIED_DESC ~Olrun's Armor~
WRITE_ASCII 0x0022 ~3a~ #2
LPF DELETE_ITEM_EQEFFECT INT_VAR silent=1 opcode_to_delete=0x7 END
LPF DELETE_ITEM_EQEFFECT INT_VAR silent=1 opcode_to_delete=0x8 END

// Armor Color
LPF ADD_ITEM_EQEFFECT INT_VAR silent=1 opcode=0x7 parameter1=0x88 parameter2=5 target=1 timing=2 END
// Strap
LPF ADD_ITEM_EQEFFECT INT_VAR silent=1 opcode=0x7 parameter1=0xa0 parameter2=4 target=1 timing=2 END
// Belt
LPF ADD_ITEM_EQEFFECT INT_VAR silent=1 opcode=0x7 parameter1=0xd6 parameter2=0 target=1 timing=2 END

// The following is the copying of Olrun's .cre file, as well as assigning his sound-set.
// See the file sndslot.ids for a listing of sounds you can add.
COPY ~%MOD_FOLDER%/cre/uxolr.cre~ ~override/uxolr.cre~

SAY NAME1 ~Olrun~
SAY NAME2 ~Olrun~

SAY INITIAL_MEETING ~Hail friend, it is I, Olrun.  Have you a moment?~ [uxolr01]
SAY MORALE ~Run away, run away!~ [uxolr02]
SAY HAPPY ~There's no group of people I'd rather be fighting alongside.~ [uxolr03]
SAY UNHAPPY_ANNOYED ~You are starting to make me lose confidence in your leadership.~ [uxolr04]
SAY UNHAPPY_SERIOUS ~Keep it up, and I'll cause you to lose something too.~ [uxolr05]
SAY UNHAPPY_BREAKING ~I gave you more than enough chances, goodbye!~ [uxolr06]
SAY LEADER ~Ah, I see I'm at the helm.  I'll lead us gloriously into battle - definitely not into danger! No sir ree~ [uxolr07]
SAY TIRED ~With or without you, I'm hittin the hay.~ [uxolr08]
SAY BORED ~If we're standing around doing a whole lotta nothing, how about a game?  I love ones with many sided dice.~ [uxolr09]
SAY BATTLE_CRY1 ~Taste my boot fool!~ [uxolr10]
SAY BATTLE_CRY2 ~Zariel and Anari, care to join me in giving this one a spankin?~ [uxolr11]
SAY BATTLE_CRY3 ~Blood and guts, blood and... Zariel, yer damn song is stuck in me head!~ [uxolr12]
SAY BATTLE_CRY4 ~Athar, you flank him while I spank him!~ [uxolr13]
SAY BATTLE_CRY5 ~Voice, don't be a stranger - you take a few swings too!~ [uxolr14]
SAY DAMAGE ~Where'd that dagger go? Argh, stuck in me back!~ [uxolr15]
SAY DYING ~Going down in the glory of battle!  I'd have it no other way.~ [uxolr16]
SAY HURT ~I think I've sustained too much damage!~ [uxolr17]
SAY AREA_FOREST ~Ah, the beauty of nature.  Now let's chop some of it down and get to campin, I love a good campfire story!~ [uxolr18]
SAY AREA_CITY ~Time to hit the tavern!  What - I'm a dwarf, what'd you expect me to say? I love a tall glass of milk!~ [uxolr19]
SAY AREA_DUNGEON ~Evil is the flame, and I am the wind.  Time to put it out.~ [uxolr20]
SAY AREA_DAY ~I think my head is burning.~ [uxolr21]
SAY AREA_NIGHT ~All those years underground really helped my eyesight for times like this.~ [uxolr22]
SAY SELECT_COMMON1 ~Yeah, what?~ [uxolr23]
SAY SELECT_COMMON2 ~Let us show our enemies the power of our strength!~ [uxolr24]
SAY SELECT_COMMON3 ~My hammer and axe is always ready for a fight!~ [uxolr25]
SAY SELECT_COMMON4 ~We must prove our courage in the face of danger!~ [uxolr26]
SAY SELECT_COMMON5 ~If our enemies stand in our way, we will crush them!~ [uxolr27]
SAY SELECT_COMMON6 ~Let us use our courage and wisdom to triumph over adversity!~ [uxolr28]
SAY SELECT_ACTION1 ~As you wish.~ [uxolr29]
SAY SELECT_ACTION2 ~Do this, do that, does it ever end?~ [uxolr30]
SAY SELECT_ACTION3 ~Beep boop, your automaton obeys.~ [uxolr31]
SAY SELECT_ACTION4 ~That's easy enough.~ [uxolr32]
SAY SELECT_ACTION5 ~All these commands are gonna make my hair fall out, oh, wait...~ [uxolr33]
SAY SELECT_ACTION6 ~Lets get to crackin some skulls.~ [uxolr34]
SAY SELECT_ACTION7 ~Yes, oh omnipresent, err - right on boss.~ [uxolr35]
SAY SELECT_RARE1 ~Did I ever tell you about this one time 2 gnomes and a dwarf walked into a bar?~ [uxolr36]
SAY SELECT_RARE2 ~Evil stands no chance with us on the job - well, you're ok Zariel.~ [uxolr37]
SAY CRITICAL_HIT ~Blown to smithereens!  The splatter is brilliant!~ [uxolr38]
SAY CRITICAL_MISS ~I'm striking out over here!~ [uxolr39]
SAY TARGET_IMMUNE ~This weapon is trash, it does nothing! Time to hit the forge!~ [uxolr40]
SAY INVENTORY_FULL ~As much as I love hoardin, I'm out of space.~ [uxolr41]
SAY SPELL_DISRUPTED ~I can't spell, and I can't seem to spell either!  I am interrupted!~ [uxolr42]
SAY REACT_TO_DIE_GENERAL ~Rest in piece friend, you will be avenged!~ [uxolr43]
SAY REACT_TO_DIE_SPECIFIC ~No!  Another one down!~ [uxolr44]
SAY SET_A_TRAP ~Watch where you step, this one's gonna be a doozy.~ [uxolr45]
SAY PICKED_POCKET ~Little bit of extra coin for me, if I do say so myself.~ [uxolr46]
SAY HIDDEN_IN_SHADOWS ~Activate dwarven ultra stealth mode.~ [uxolr47]

SAY ATTACK1 ~My fist, your face.  Your wrist, my mace.~ [uxolr48]
SAY ATTACK2 ~It's slobbering time!  Hmm, sounded better in my head.~ [uxolr49]
SAY ATTACK3 ~Hack and slash, that's all it is.~ [uxolr50]
SAY ATTACK4 ~I honor my Gods, by destroying yours.~ [uxolr51]



WRITE_LONG  0x10 0x0 // clear all flags
WRITE_ASCII 0x248 ~uxolrs~ #8  // override script where you put all the commands for lovetalks and such.
WRITE_ASCII 0x2cc ~uxolr~  #8  // dialogue file where we'll put Branwen's greeting dialogue.
WRITE_ASCII 0x280 ~uxolr~  #32 // death variable - when we want to refer to Branwen in the game, we'll call her O#Bran. For example, InParty("O#Bran") means that Branwen is in party.
WRITE_ASCII 0x250 ~~ 	    #8  // class script
WRITE_ASCII 0x258 ~~ 	    #8  // race script
WRITE_ASCII 0x260 ~~ 	    #8  // general script
WRITE_ASCII 0x268 ~~	    #8	// default script
WRITE_ASCII 0X34  ~uxolrs~ #8  // small portrait
WRITE_ASCII 0x3c  ~uxolrm~ #8  // medium portrait

WRITE_LONG 0x28  0x6102 // FIGHTER_MALE_DWARF (https://gibberlings3.github.io/iesdp/files/ids/bgee/animate.htm)
WRITE_BYTE 0x272 0x4 // Dwarf (https://gibberlings3.github.io/iesdp/files/ids/bg2/race.htm)
WRITE_BYTE 0x273 0x2 // Fighter (https://gibberlings3.github.io/iesdp/files/ids/bg2/class.htm)
WRITE_BYTE 0x275 0x1 // Male (https://gibberlings3.github.io/iesdp/files/ids/bg2/gender.htm)
WRITE_LONG 0x244 0x00004000 // Kit Barbarian (https://gibberlings3.github.io/iesdp/file_formats/ie_formats/cre_v1.htm)
WRITE_BYTE 0x270 128 // Neutral (https://gibberlings3.github.io/iesdp/files/ids/bg2/ea.htm)

// Use eekeeper to see the codes, looks like a color index
WRITE_BYTE 0x2c  0x81 // Metal Colour
WRITE_BYTE 0x2d  0xd6 // Minor Colour
WRITE_BYTE 0x2e  0x81 // Major Colour
WRITE_BYTE 0x2f  0x0c // skin Colour
WRITE_BYTE 0x30  0x81 // Leather Colour
WRITE_BYTE 0x31  0x81 // Armor Colour
WRITE_BYTE 0x32  0x72 // Hair Colour

ADD_CRE_ITEM ~uxham01~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP // TWOHANDED
ADD_CRE_ITEM ~ax1h02~ #0 #0 #0 ~IDENTIFIED~ ~SHIELD~ EQUIP // TWOHANDED
ADD_CRE_ITEM ~uxlea02~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~ EQUIP // TWOHANDED
ADD_CRE_ITEM ~uxhel02~ #0 #0 #0 ~IDENTIFIED~ ~HELMET~ EQUIP // TWOHANDED

COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialogues/uxbolr.d~ // All Branwen's banters with other party members will go here. We need the "EVALUATE_BUFFER" because the chapter variable now uses the OUTER_SPRINT variable defined above for the EET continuous journal system.
	~%MOD_FOLDER%/dialogues/uxolr.d~  // Branwen's greeting dialogue will go here.
	~%MOD_FOLDER%/dialogues/uxolrj.d~ // The rest of Branwen's dialogue: her commentary, interjections, talks with the player, lovetalks and everything else.
	~%MOD_FOLDER%/dialogues/uxolrp.d~ // Branwen's leaving party dialogue will go here.

	~%MOD_FOLDER%/scripts/uxolrs.baf~ // Branwen's script.
	~%MOD_FOLDER%/scripts/uxolrd.baf~ // Branwen's dream script for events at rest. If you want your NPC to talk to you at rest, add scripting here.
	~%MOD_FOLDER%/scripts/uxolrf.baf~ // Branwen's "fixing" script. Remember how various modders advised you to "select your NPC and press K"? Well, that's how they do it.

// Now we are going to add Branwen content for the Throne of Bhaal. You need one more cre file, or you could use the same one twice.
ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN // It means that the Throne of Bhaal is installed. If not, this part is not added to the game. This check also works for BG2:EE games.
BEGIN // you WILL need an END below.

COPY ~%MOD_FOLDER%/cre/uxolr25.cre~ ~override/uxolr25.cre~
SAY NAME1 ~Olrun~
SAY NAME2 ~Olrun~

SAY SELECT_COMMON1 ~Yes?~ [uxolr01]
SAY SELECT_COMMON2 ~Let's work together, and I'm sure we can make it through this challenge!~ [uxolr02]
SAY SELECT_COMMON3 ~No matter what obstacles stand in our way, we will prevail!~ [uxolr03]
SAY SELECT_COMMON4 ~We must stay alert and remain vigilant against those who would seek to use the Heart for ill.~ [uxolr04]
SAY SELECT_COMMON5 ~Let us bravely march forward and protect Baldur's Gate!~ [uxolr05]
SAY SELECT_COMMON6 ~We mustn't give up nowâ€”we're so close to achieving our goal!~ [uxolr06]

SAY SELECT_ACTION1 ~On it.~ [uxolr07]
SAY SELECT_ACTION2 ~As you wish.~ [uxolr08]
SAY SELECT_ACTION3 ~Yea yea.~ [uxolr09]
SAY SELECT_ACTION4 ~If I must.~ [uxolr10]
SAY SELECT_ACTION5 ~Athar was never so bossy.~ [uxolr11]
SAY SELECT_ACTION6 ~Oooooookay~ [uxolr12]
SAY SELECT_ACTION7 ~Yep yep yep, going going going.~ [uxolr13]

WRITE_LONG  0x10 0x0 // clear all flags
WRITE_ASCII 0x248 ~uxolrs~ #8  // override script where you put all the commands for lovetalks and such.
WRITE_ASCII 0x2cc ~uxolr~  #8  // dialogue file where we'll put Branwen's greeting dialogue.
WRITE_ASCII 0x280 ~uxolr~  #32 // death variable - when we want to refer to Branwen in the game, we'll call her O#Bran. For example, InParty("O#Bran") means that Branwen is in party.
WRITE_ASCII 0x250 ~~ 	    #8  // class script
WRITE_ASCII 0x258 ~~ 	    #8  // race script
WRITE_ASCII 0x260 ~~ 	    #8  // general script
WRITE_ASCII 0x268 ~~	    #8	// default script
WRITE_ASCII 0X34  ~olruns~ #8  // small portrait
WRITE_ASCII 0x3c  ~olrunm~ #8  // medium portrait

WRITE_LONG 0x28  0x6102 // FIGHTER_MALE_DWARF (https://gibberlings3.github.io/iesdp/files/ids/bgee/animate.htm)
WRITE_BYTE 0x272 0x4 // Dwarf (https://gibberlings3.github.io/iesdp/files/ids/bg2/race.htm)
WRITE_BYTE 0x273 0x2 // Fighter (https://gibberlings3.github.io/iesdp/files/ids/bg2/class.htm)
WRITE_BYTE 0x275 0x1 // Male (https://gibberlings3.github.io/iesdp/files/ids/bg2/gender.htm)


COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/dialogues/uxbolr25.d~  // All Branwen's ToB banters with other party members will go here. Again, we use 25, because it's a tradution for ToB. Again, "EVALUATE_BUFFER" because of the chapter numbers
	~%MOD_FOLDER%/dialogues/uxolr25.d~  // Branwen's ToB greeting dialogue will go here.
	~%MOD_FOLDER%/dialogues/uxolr25j.d~  // The rest of Branwen's ToB dialogue: her commentary, interjections, talks with the player, lovetalks and everything else.
	~%MOD_FOLDER%/dialogues/uxolr25p.d~  // Branwen's ToB leaving party dialogue will go here.

	~%MOD_FOLDER%/scripts/uxolr25s.baf~ // Branwen's script for ToB.
	~%MOD_FOLDER%/scripts/uxolr25d.baf~ // Branwen's dream script for ToB.

END // and here ends ToB content

LAF dialog_tables STR_VAR code = "olr" END

// EET related code
ACTION_IF GAME_IS ~eet~ BEGIN //only true if game is detected as Enhanced Edition Trilogy (mod for BG2:EE)
  INCLUDE ~EET/other/EET_functions.tph~ //declaration of external EET functions used below. This is part of the EET package the player has in his install so don't worry about it
  LAF ~EET_NPC_TRANSITION~ //function used to automatically implement EET continuity system. This deals with the transition to ToB and automatically creates all the needed script blocks and dialogue entries for e.g. the calling of the SoA Branwen the player left somewhere via the fate spirit, the disabling of the fate spirit calling if Branwen is in the party upon transition etc. The function will be executed upon installation of the mod. What follows is all entries we need for our Branwen:
    INT_VAR
      type = 3 //informs the function that this is BG2 NPC without BG1 content (because the mod doesn't add to BG1)
      default_ToB = 1 //NPC available as summonable NPC when the game is started in ToB (= new ToB game)
      clean_ToB = 1 //forces function to clean old BG1 Branwen code that exists in AR4000.BCS and FATESP.DLG (i.e. no entry for the original BG1 Branwen if this mod is installed)
    STR_VAR
      dv = "uxolr" //Branwen Death Variable (script name)
      override_BG1 = "uxolrs" //Branwen SoA Override script
      override_SoD = "uxolrs" //Branwen SoA Override script
      override_SoA = "uxolrs" //Branwen SoA Override script
      override_ToB = "uxolr25s" //Branwen ToB Override script
      dialog_ToB = "uxolr25" //Branwen ToB dialogue file
      cre_ToB = "uxolr25" //Branwen ToB CRE filename
      traFile = EVAL "%MOD_FOLDER%/tra/%LANGUAGE%/fatesp.tra"
      string = "@0" //from the TRA file specified via traFile
      stringPosDV = "Cernd" //Branwen will be appended right above Cernd
      clean_ToB_DV = "Olrun" //death variable of BG1 Branwen (see clean_ToB comment)
  END
END ELSE BEGIN //we use different Fate Spirit dialogue patching for any other platform than EET
  ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN // Just add the fate stuff if we are on tob
  BEGIN
    COMPILE ~%MOD_FOLDER%/dialogues/fatesp.d~ USING "%MOD_FOLDER%/tra/%LANGUAGE%/fatesp.tra"
  END
END

END
