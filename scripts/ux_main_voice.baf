// Be on standby for a group join
IF
See([PC])
!Global("ux_group_join_deny", "GLOBAL", 1)
Global("ux_prelude_done","GLOBAL",1)
Global("ux_request_group_join","GLOBAL", 1)
!Global("ux_in_party_voice", "LOCALS", 1)
!Global("ux_in_party_once_voice", "LOCALS", 1)
THEN RESPONSE #100
SetGlobal("ux_in_party_voice", "LOCALS", 1)
SetGlobal("ux_in_party_once_voice", "LOCALS", 1)
RealSetGlobalTimer("ux_voice_banter_timer", "GLOBAL", 5)
JoinParty()
END

// Summon the special messenger
IF
!Global("ux_urgent_spawn", "GLOBAL", 1)
OR(5)
InParty("uxana")
InParty("uxath")
InParty("uxvoi")
InParty("uxzar")
InParty("uxolr")
THEN
RESPONSE #100
SetGlobal("ux_urgent_spawn", "GLOBAL", 1)
CreateCreatureObjectOffScreen("uxpea",Player1,0,0,0)
Continue()
END

// Try to initiate some chit chat
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
RealGlobalTimerExpired("ux_voice_banter_timer","GLOBAL")
OR(2)
Global("ux_voice_banter_id", "GLOBAL", 0)
Global("ux_voice_banter_id", "GLOBAL", 1)
THEN
RESPONSE #100
PlaySong(165)
SetGlobal("ux_voice_is_bantering", "GLOBAL", 1)
RealSetGlobalTimer("ux_voice_banter_timer", "GLOBAL", 5)
StartDialogueNoSet(Player1)
SetGlobal("ux_voice_is_bantering", "GLOBAL", 0)
END

// Pid stuff
IF
Global("ux_voice_wishes_to_pid", "GLOBAL", 1)
THEN
RESPONSE #100
PlaySong(165)
SetGlobal("ux_voice_wishes_to_pid", "GLOBAL", 0)
SetGlobal("ux_voice_is_pidding", "GLOBAL", 1)
StartDialogueNoSet(Player1)
SetGlobal("ux_voice_is_pidding", "GLOBAL", 0)
END
