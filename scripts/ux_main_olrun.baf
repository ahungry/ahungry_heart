// Open up initial dialogue and then leave until later
IF
See([PC])
Global("ux_state", "GLOBAL", 0)
// !Global("ux_prelude_done", "GLOBAL", 1)
THEN
RESPONSE #100
PlaySong(164) // See override/songlist.2da
Dialogue(Nearest([PC]))
IncrementGlobal("ux_state", "GLOBAL", 10)
END

// // Be on standby for a group join
// IF
// See([PC])
// !Global("ux_group_join_deny", "GLOBAL", 1)
// Global("ux_prelude_done","GLOBAL",1)
// Global("ux_request_group_join","GLOBAL", 1)
// !Global("Ux_in_party_olrun", "LOCALS", 1)
// !Global("Ux_in_party_once_olrun", "LOCALS", 1)
// THEN RESPONSE #100
// SetGlobal("Ux_in_party_olrun", "LOCALS", 1)
// SetGlobal("Ux_in_party_once_olrun", "LOCALS", 1)
// //RealSetGlobalTimer("ux_olrun_banter_timer", "GLOBAL", 300)
// JoinParty()
// END

// Summon the special messenger
IF
Global("ux_state", "GLOBAL", 30)
RealGlobalTimerExpired("ux_urgent_timer","GLOBAL")
!InParty("uxana")
!InParty("uxath")
InParty("uxolr")
THEN
RESPONSE #100
IncrementGlobal("ux_state", "GLOBAL", 10)
CreateCreatureObjectOffScreen("uxpea",Player1,0,0,0)
Continue()
END

// Split apart to head back to Baldur's Gate
IF
GlobalGT("ux_state", "GLOBAL", 49)
GlobalLT("ux_state", "GLOBAL", 60)
!Global("ux_keep_uxolr", "GLOBAL", 1)
THEN
RESPONSE #100
IncrementGlobal("ux_state", "GLOBAL", 1)
SetGlobal("ux_in_party_uxolr", "GLOBAL", 0)
ChangeAIScript("",DEFAULT)
SetLeavePartyDialogFile()
LeaveParty()
// Actually go to Durlag's Tower and admire it (You can use NWW )
EscapeAreaObjectMove("%BG_PRE%0500", "North", 850, 280, SE)
Continue()
END

// At this point, each member has reliably left, and we bump to state 60, more open ended
IF
Global("ux_state", "GLOBAL", 54)
THEN
RESPONSE #100
SetGlobal("ux_state", "GLOBAL", 60)
Continue()
END

// Summon the special messenger for the second message
IF
Global("ux_state", "GLOBAL", 60)
GlobalTimerExpired("ux_bg_is_closed_timer","GLOBAL")
!InParty("uxana")
!InParty("uxath")
InParty("uxolr")
THEN
RESPONSE #100
CreateCreatureObjectOffScreen("uxpea",Player1,0,0,0)
// Keep running this till it happens
SetGlobalTimer("ux_bg_is_closed_timer", "GLOBAL", SEVEN_ROUNDS)
Continue()
END

// Try to initiate some chit chat
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
RealGlobalTimerExpired("ux_olrun_banter_timer","GLOBAL")
OR(2)
Global("ux_olrun_banter_id", "GLOBAL", 0)
Global("ux_olrun_banter_id", "GLOBAL", 1)
THEN
RESPONSE #100
PlaySong(168)
SetGlobal("ux_olrun_is_bantering", "GLOBAL", 1)
RealSetGlobalTimer("ux_olrun_banter_timer", "GLOBAL", 5)
StartDialogueNoSet(Player1)
SetGlobal("ux_olrun_is_bantering", "GLOBAL", 0)
Continue()
END

// Pid stuff
IF
Global("ux_olrun_wishes_to_pid", "GLOBAL", 1)
THEN
RESPONSE #100
PlaySong(168)
SetGlobal("ux_olrun_wishes_to_pid", "GLOBAL", 0)
SetGlobal("ux_olrun_is_pidding", "GLOBAL", 1)
StartDialogueNoSet(Player1)
SetGlobal("ux_olrun_is_pidding", "GLOBAL", 0)
Continue()
END
