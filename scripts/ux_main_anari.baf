// Be on standby for a group join
// IF
// See([PC])
// !Global("ux_group_join_deny", "GLOBAL", 1)
// Global("ux_prelude_done","GLOBAL",1)
// Global("ux_request_group_join","GLOBAL", 1)
// !Global("ux_in_party_anari", "LOCALS", 1)
// !Global("ux_in_party_once_anari","LOCALS",1)
// THEN RESPONSE #100
// SetGlobal("ux_in_party_anari", "LOCALS", 1)
// SetGlobal("ux_in_party_once_anari", "LOCALS", 1)
// // RealSetGlobalTimer("ux_anari_banter_timer", "GLOBAL", 300)
// JoinParty()
// END

// Summon the special messenger
IF
Global("ux_state", "GLOBAL", 30)
RealGlobalTimerExpired("ux_urgent_timer","GLOBAL")
OR(5)
InParty("uxana")
InParty("uxath")
InParty("uxvoi")
InParty("uxzar")
InParty("uxolr")
THEN
RESPONSE #100
IncrementGlobal("ux_state", "GLOBAL", 10)
CreateCreatureObjectOffScreen("uxpea",Player1,0,0,0)
Continue()
END

// Split apart to head back to Baldur's Gate
IF
GlobalGT("ux_state", "GLOBAL", 49)
GlobalLT("ux_state", "GLOBAL", 60)
!Global("ux_keep_uxana", "GLOBAL", 1)
THEN
RESPONSE #100
IncrementGlobal("ux_state", "GLOBAL", 1)
SetGlobal("ux_in_party_uxana", "GLOBAL", 0)
ChangeAIScript("",DEFAULT)
SetLeavePartyDialogFile()
LeaveParty()
// Actually go to Kivan
EscapeAreaObjectMove("%BG_PRE%3200", "North", 3712, 1474, 5)
Continue()
END

// At this point, each member has reliably left, and we bump to state 60, more open ended
IF
Global("ux_state", "GLOBAL", 54)
THEN
RESPONSE #100
SetGlobal("ux_state", "GLOBAL", 60)
Continue()
END

// Try to initiate some chit chat
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
RealGlobalTimerExpired("ux_anari_banter_timer","GLOBAL")
OR(2)
Global("ux_anari_banter_id", "GLOBAL", 0)
Global("ux_anari_banter_id", "GLOBAL", 1)
THEN
RESPONSE #100
PlaySong(167)
SetGlobal("ux_anari_is_bantering", "GLOBAL", 1)
RealSetGlobalTimer("ux_anari_banter_timer", "GLOBAL", 5)
StartDialogueNoSet(Player1)
SetGlobal("ux_anari_is_bantering", "GLOBAL", 0)
Continue()
END

// Pid stuff
IF
Global("ux_anari_wishes_to_pid", "GLOBAL", 1)
THEN
RESPONSE #100
PlaySong(167)
SetGlobal("ux_anari_wishes_to_pid", "GLOBAL", 0)
SetGlobal("ux_anari_is_pidding", "GLOBAL", 1)
StartDialogueNoSet(Player1)
SetGlobal("ux_anari_is_pidding", "GLOBAL", 0)
Continue()
END
